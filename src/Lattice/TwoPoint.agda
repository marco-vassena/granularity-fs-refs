-- In this module we give a simple instantion of the 2-points lattice,
-- ğ‘³ = {Low, High}, where High â‹¤ Low is the only disallowed flow.  We
-- define the label set, the can-flow-to relation, join and meet
-- operations.
--
-- We then proof that this structure is a Lattice according to the
-- definition given in module Lattice.

module Lattice.TwoPoint where

open import Lattice using (Lattice ; IsLattice)
open import Relation.Nullary
open import Relation.Binary.PropositionalEquality hiding (isPreorder)
open import Data.Empty

-- Low and High labels.
data Label : Set where
 Low : Label
 High : Label

-- Decidable equality over Label
_â‰Ÿ_ : (lâ‚ lâ‚‚ : Label) -> Dec (lâ‚ â‰¡ lâ‚‚)
Low â‰Ÿ Low = yes refl
Low â‰Ÿ High = no (Î» ())
High â‰Ÿ Low = no (Î» ())
High â‰Ÿ High = yes refl

-- Join
_âŠ”_ : Label â†’ Label â†’ Label
Low âŠ” â„“â‚‚ = â„“â‚‚
High âŠ” â„“â‚‚ = High

infixr 5 _âŠ”_

-- Meet
_âŠ“_ : Label â†’ Label â†’ Label
Low âŠ“ y = Low
High âŠ“ y = y

infixr 5 _âŠ“_

-- Flow-to relation over Labels.
data _âŠ‘_ : Label -> Label -> Set where
 LâŠ‘ : âˆ€ (l : Label) -> Low âŠ‘ l
 HâŠ‘H : High âŠ‘ High

-- High is the top of the lattice.
_âŠ‘H : âˆ€ (â„“ : Label) â†’ â„“ âŠ‘ High
Low âŠ‘H = LâŠ‘ High
High âŠ‘H = HâŠ‘H

-- âŠ‘ is decidable.
_âŠ‘?_ : (lâ‚ lâ‚‚ : Label) -> Dec (lâ‚ âŠ‘ lâ‚‚)
Low âŠ‘? lâ‚‚ = yes (LâŠ‘ lâ‚‚)
High âŠ‘? Low = no (Î» ())
High âŠ‘? High = yes HâŠ‘H

-- âŠ‘ is reflexive.
refl-âŠ‘ : âˆ€ {l} -> l âŠ‘ l
refl-âŠ‘ {Low} = LâŠ‘ Low
refl-âŠ‘ {High} = HâŠ‘H

-- âŠ‘ is antisymmetric.
antisym-âŠ‘ : âˆ€ {x y} â†’ x âŠ‘ y â†’ y âŠ‘ x â†’ x â‰¡ y
antisym-âŠ‘ (LâŠ‘ .Low) (LâŠ‘ .Low) = refl
antisym-âŠ‘ HâŠ‘H b = refl

-- âŠ‘ is transitive.
trans-âŠ‘ : âˆ€ {lâ‚ lâ‚‚ lâ‚ƒ} -> lâ‚ âŠ‘ lâ‚‚ -> lâ‚‚ âŠ‘ lâ‚ƒ -> lâ‚ âŠ‘ lâ‚ƒ
trans-âŠ‘ (LâŠ‘ .Low) (LâŠ‘ lâ‚ƒ) = LâŠ‘ lâ‚ƒ
trans-âŠ‘ (LâŠ‘ .High) HâŠ‘H = LâŠ‘ High
trans-âŠ‘ HâŠ‘H HâŠ‘H = HâŠ‘H

--------------------------------------------------------------------------------
-- Algebraic properties of join _âŠ”_

sym-âŠ” : âˆ€ (â„“â‚ â„“â‚‚ : Label) â†’ (â„“â‚ âŠ” â„“â‚‚) â‰¡ (â„“â‚‚ âŠ” â„“â‚)
sym-âŠ” Low Low = refl
sym-âŠ” Low High = refl
sym-âŠ” High Low = refl
sym-âŠ” High High = refl

assoc-âŠ” : âˆ€ â„“â‚ â„“â‚‚ â„“â‚ƒ â†’ (â„“â‚ âŠ” â„“â‚‚) âŠ” â„“â‚ƒ â‰¡ â„“â‚ âŠ” â„“â‚‚ âŠ” â„“â‚ƒ
assoc-âŠ” Low â„“â‚‚ â„“â‚ƒ = refl
assoc-âŠ” High â„“â‚‚ â„“â‚ƒ = refl

--------------------------------------------------------------------------------
-- Algebraic properties of meet _âŠ“_

sym-âŠ“ : âˆ€ (â„“â‚ â„“â‚‚ : Label) â†’ (â„“â‚ âŠ“ â„“â‚‚) â‰¡ (â„“â‚‚ âŠ“ â„“â‚)
sym-âŠ“ Low Low = refl
sym-âŠ“ Low High = refl
sym-âŠ“ High Low = refl
sym-âŠ“ High High = refl

assoc-âŠ“ : âˆ€ â„“â‚ â„“â‚‚ â„“â‚ƒ â†’ (â„“â‚ âŠ“ â„“â‚‚) âŠ“ â„“â‚ƒ â‰¡ â„“â‚ âŠ“ â„“â‚‚ âŠ“ â„“â‚ƒ
assoc-âŠ“ Low â„“â‚‚ â„“â‚ƒ = refl
assoc-âŠ“ High â„“â‚‚ â„“â‚ƒ = refl

--------------------------------------------------------------------------------
-- Ordering properties of âŠ‘ and âŠ”

join-âŠ‘â‚ : âˆ€ â„“â‚ â„“â‚‚ â†’ â„“â‚ âŠ‘ (â„“â‚ âŠ” â„“â‚‚)
join-âŠ‘â‚ Low â„“â‚‚ = LâŠ‘ â„“â‚‚
join-âŠ‘â‚ High â„“â‚‚ = HâŠ‘H

join-âŠ‘â‚‚ : âˆ€ â„“â‚ â„“â‚‚ â†’ â„“â‚‚ âŠ‘ (â„“â‚ âŠ” â„“â‚‚)
join-âŠ‘â‚‚ x y with join-âŠ‘â‚ y x
... | p rewrite sym-âŠ” x y = p

join-âŠ‘â‚ƒ : âˆ€ x y z â†’ x âŠ‘ z â†’ y âŠ‘ z â†’ (x âŠ” y) âŠ‘ z
join-âŠ‘â‚ƒ .Low y z (LâŠ‘ .z) b = b
join-âŠ‘â‚ƒ .High y .High HâŠ‘H b = HâŠ‘H

--------------------------------------------------------------------------------
-- Ordering properties of âŠ‘ and âŠ“

meet-âŠ‘â‚ : âˆ€ x y â†’ (x âŠ“ y) âŠ‘ x
meet-âŠ‘â‚ Low y = LâŠ‘ Low
meet-âŠ‘â‚ High y = y âŠ‘H

meet-âŠ‘â‚‚ : âˆ€ x y â†’ (x âŠ“ y) âŠ‘ y
meet-âŠ‘â‚‚ Low y = LâŠ‘ y
meet-âŠ‘â‚‚ High y = refl-âŠ‘

meet-âŠ‘â‚ƒ : âˆ€ x y z â†’ z âŠ‘ x â†’ z âŠ‘ y â†’ z âŠ‘ (x âŠ“ y)
meet-âŠ‘â‚ƒ x y .Low (LâŠ‘ .x) b = LâŠ‘ (x âŠ“ y)
meet-âŠ‘â‚ƒ .High y .High HâŠ‘H b = b

--------------------------------------------------------------------------------
-- Lattice instance

open import Relation.Binary
import Relation.Binary.Lattice as R
open import Data.Product
import Algebra.Structures as A
open import Algebra.FunctionProperties (_â‰¡_ {A = Label})

supremum : R.Supremum _âŠ‘_ _âŠ”_
supremum x y = join-âŠ‘â‚ x y , join-âŠ‘â‚‚ x y , join-âŠ‘â‚ƒ x y

infimum : R.Infimum _âŠ‘_ _âŠ“_
infimum = Î» x y â†’ meet-âŠ‘â‚ x y , meet-âŠ‘â‚‚ x y , meet-âŠ‘â‚ƒ x y

isPreorder : IsPreorder _â‰¡_ _âŠ‘_
isPreorder =
  record { isEquivalence = isEquivalence
         ; reflexive =  Î» { refl â†’ refl-âŠ‘ }
         ; trans = trans-âŠ‘ }

isPartialOrder : IsPartialOrder _â‰¡_ _âŠ‘_
isPartialOrder =
  record { isPreorder = isPreorder
         ; antisym = antisym-âŠ‘ }

isDecPartialOrder : IsDecPartialOrder _â‰¡_ _âŠ‘_
isDecPartialOrder =
  record { isPartialOrder = isPartialOrder
         ; _â‰Ÿ_ = _â‰Ÿ_
         ; _â‰¤?_ = _âŠ‘?_ }

âŠ”-absorbs-âŠ“ : _âŠ”_ Absorbs _âŠ“_
âŠ”-absorbs-âŠ“ Low y = refl
âŠ”-absorbs-âŠ“ High y = refl

âŠ“-absorbs-âŠ” : _âŠ“_ Absorbs _âŠ”_
âŠ“-absorbs-âŠ” Low y = refl
âŠ“-absorbs-âŠ” High y = refl

absorptive : Absorptive _âŠ”_ _âŠ“_
absorptive = âŠ”-absorbs-âŠ“ , âŠ“-absorbs-âŠ”

isLattice : A.IsLattice _â‰¡_ _âŠ”_ _âŠ“_
isLattice = record
              { isEquivalence = isEquivalence
              ; âˆ¨-comm = sym-âŠ”
              ; âˆ¨-assoc = assoc-âŠ” -- Î» x y z â†’ sym (assoc-âŠ” x y z)
              ; âˆ¨-cong = Î» { refl refl â†’ refl }
              ; âˆ§-comm = sym-âŠ“
              ; âˆ§-assoc = assoc-âŠ“
              ; âˆ§-cong = Î» { refl refl â†’ refl }
              ; absorptive = absorptive
              }

isSecLattice : IsLattice _âŠ‘_ _âŠ”_ _âŠ“_
isSecLattice = record
              { supremum = supremum
              ; infimum = infimum
              ; isDecPartialOrder = isDecPartialOrder
              ; isLattice = isLattice
              }

-- ğ‘³Â² is the 2-point lattice, where High â‹¤ Low is the only disallowed
-- flow.
instance
  ğ‘³Â² : Lattice
  ğ‘³Â² = record
            { Carrier = Label
            ; _â‰¤_ = _âŠ‘_
            ; _âˆ¨_ = _âŠ”_
            ; _âˆ§_ = _âŠ“_
            ; isSecurityLattice = isSecLattice
            }
